import java.util.function.Predicate

/*
 * Copyright Â© 2019 - 2024  Kynetics, Inc.
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'com.android.application'

android {
    namespace 'com.kynetics.can_example_application'

    compileSdk 34
    defaultConfig {
        applicationId "com.kynetics.can_example_application"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 5
        versionName "1.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def KyneticsKeystorePass = System.getenv("KYNETICS_KEYSTORE_PASS")
    def KyneticsKeyAlias = System.getenv("KYNETICS_KEY_ALIAS")
    def KyneticsKeyPass = System.getenv("KYNETICS_KEY_PASS")

    def envs= [KyneticsKeystorePass, KyneticsKeyAlias, KyneticsKeyPass]
    def signWithKyneticsKey = envs.stream().allMatch(new Predicate<String>() {
        @Override
        boolean test(String s) {
            return s != null && !s.isEmpty()
        }
    })

    signingConfigs{
        if (signWithKyneticsKey) {
            kynetics {
                storePassword KyneticsKeystorePass
                keyAlias KyneticsKeyAlias
                keyPassword KyneticsKeyPass
                storeFile file('../keystore/kynetics.jks')
                v2SigningEnabled true
                v1SigningEnabled true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        if (signWithKyneticsKey) {
            kynetics_key {
                signingConfig signingConfigs.kynetics
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                matchingFallbacks = ['release', 'debug']
            }
        }
    }

    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar'])
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.23'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
